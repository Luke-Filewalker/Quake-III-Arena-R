#version 460
#extension GL_GOOGLE_include_directive    : enable
#extension GL_EXT_nonuniform_qualifier    : enable
#include "../constants.h"
#include "asvgf.glsl"
#include "../raytracer/gbuffer.glsl"
#include "../raytracer/rt_Helper.glsl"
#include "../raytracer/defines.glsl"
#include "../raytracer/vertexData.glsl"
#include "../constants.h"

layout(binding = BINDING_OFFSET_BLUE_NOISE, set = 0) uniform sampler2DArray blue_noise;


layout(binding = BINDING_OFFSET_GLOBAL_UBO, set = 0) uniform global_ubo
{
	GlobalUbo ubo;
};


layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

float
srgb_to_linear(float srgb)
{
    if(srgb <= 0.04045f) {
        return srgb * (1.0f / 12.92f);
    }
    else {
        return pow((srgb + 0.055f) * (1.0f / 1.055f), 2.4f);
    }
}

vec3
srgb_to_linear(vec3 srgb)
{
    return vec3(
            srgb_to_linear(srgb.x),
            srgb_to_linear(srgb.y),
            srgb_to_linear(srgb.z));
}
vec3
viridis_quintic(float x)
{
    x = clamp(x, 0.0, 1.0);
    vec4 x1 = vec4(1.0, x, x * x, x * x * x); // 1 x x2 x3
    vec4 x2 = x1 * x1.w * x; // x4 x5 x6 x7
    return srgb_to_linear(vec3(
        dot(x1.xyzw, vec4(+0.280268003, -0.143510503, +2.225793877,  -14.815088879)) + dot(x2.xy, vec2(+25.212752309, -11.772589584)),
        dot(x1.xyzw, vec4(-0.002117546, +1.617109353, -1.909305070,  +2.701152864 )) + dot(x2.xy, vec2(-1.685288385,  +0.178738871 )),
        dot(x1.xyzw, vec4(+0.300805501, +2.614650302, -12.019139090, +28.933559110)) + dot(x2.xy, vec2(-33.491294770, +13.762053843))));
}

float
get_antilag_alpha(sampler2D tex_gradient, ivec2 ipos)
{
	const int r = 1;

	float antilag_alpha = 0.0;

	for(int yy = -r; yy <= r; yy++) {
		for(int xx = -r; xx <= r; xx++) {
			ivec2 p = ipos / GRAD_DWN + ivec2(xx, yy);

			if(any(lessThan(p, vec2(0)))
			|| any(greaterThanEqual(p, ivec2(ubo.width, ubo.height) / GRAD_DWN)))
				continue;

			float a = texelFetch(tex_gradient, p, 0).r;
			//vec2 gradient = texelFetch(tex_gradient, p, 0).rg;

			//float a = gradient.g > 1e-2 ? abs(gradient.r) / gradient.g : 0.0;
			antilag_alpha = max(antilag_alpha, a);
		}
	}

	return clamp(antilag_alpha, 0.0, 1.0);
}

void
temporal_filter()
{
	ivec2 ipos = ivec2(gl_GlobalInvocationID);
	vec4 motion = imageLoad(motionGBuffer, ipos);
	vec4  color_curr = imageLoad(IMG_GBUFFER_ILLUMINATION, ipos);
	float lum_curr   = luminance(color_curr.rgb);

	/* env map */
	if(motion.a < 0.0 || color_curr.a < 1.0) {
		imageStore(IMG_ASVGF_HIST_MOM, ipos, vec4(0));
		imageStore(IMG_ASVGF_ATROUS_A, ipos, color_curr);
		return;
	}

	motion.xy *= vec2(ubo.width, ubo.height) * 0.5;

	/* need a bit of jitter, variance estimate is changed by the subpixel
	 * error introduced from the reprojection. With the rasterizer this
	 * was automatically happening due to the global jitter */
	vec2 jitter;
	jitter.x = fract(texelFetch(blue_noise, ivec3(ipos, ubo.frameIndex + 0) % ivec3(BLUE_NOISE_RES, BLUE_NOISE_RES, NUM_BLUE_NOISE_TEX), 0).r);
	jitter.y = fract(texelFetch(blue_noise, ivec3(ipos, ubo.frameIndex + 1) % ivec3(BLUE_NOISE_RES, BLUE_NOISE_RES, NUM_BLUE_NOISE_TEX), 0).r);
	jitter -= vec2(0.5);

	vec2 pos_prev = vec2(ipos) + vec2(0.5) + motion.xy + jitter * 0.1;

	float depth_curr = imageLoad(normalGBuffer, ipos).w;
 	vec3  normal_curr = imageLoad(normalGBuffer, ipos).xyz;

	vec4 color_prev           = vec4(0);
	vec3 moments_histlen_prev = vec3(0);
	float sum_w = 0.0;
	{
		vec2 pos_ld = floor(pos_prev - vec2(0.5));
		vec2 subpix = fract(pos_prev - vec2(0.5) - pos_ld);

		const ivec2 off[4] = { { 0, 0 }, { 1, 0 }, { 0, 1 }, { 1, 1 } };
		float w[4] = {
			(1.0 - subpix.x) * (1.0 - subpix.y),
			(subpix.x      ) * (1.0 - subpix.y),
			(1.0 - subpix.x) * (subpix.y      ),
			(subpix.x      ) * (subpix.y      )
		};
		for(int i = 0; i < 4; i++) {
			ivec2 p = ivec2(pos_ld) + off[i];

			float depth_prev = imageLoad(normalGBufferPrev, p).w;
 			vec3  normal_prev = imageLoad(normalGBufferPrev, p).xyz;

			float dist_depth = abs(depth_curr - depth_prev + motion.z) * motion.a;
			if(dist_depth < 2.0 && depth_prev > 0.0 && depth_curr > 0.0) {
				color_prev           += imageLoad(IMG_ASVGF_HIST_COLOR_PREV,        p)     * w[i];
				moments_histlen_prev += imageLoad(IMG_ASVGF_HIST_MOM_PREV, p).rgb * w[i];
				sum_w                += w[i];
			}
		}
	}

	if(sum_w > 0.001 && color_curr.a > 0.0
	&& !any(isnan(color_prev)) && !any(isnan(moments_histlen_prev)))
	{

		float antilag_alpha = get_antilag_alpha(TEX_ASVGF_GRAD_B, ipos);


		color_prev           /= sum_w;
		moments_histlen_prev /= sum_w;

		float hist_len      = min(moments_histlen_prev.z + 1.0, 256.0);
		float alpha_color   = max(0.075, 1.0 / hist_len);
		float alpha_moments = max(0.1,   1.0 / hist_len);

		alpha_color   = mix(alpha_color,   1.0, antilag_alpha);
		alpha_moments = mix(alpha_moments, 1.0, antilag_alpha);

		vec2 mom_curr = vec2(lum_curr, lum_curr * lum_curr);
		vec2 mom = mix(moments_histlen_prev.xy, mom_curr, alpha_moments);
		imageStore(IMG_ASVGF_HIST_MOM, ipos, vec4(mom, hist_len, 0));

		vec4 color_var;
	   	color_var.rgb = mix(color_prev.rgb, color_curr.rgb, alpha_color);
		color_var.a   = sqrt(max(mom.y - mom.x * mom.x, 0.0));
		color_var.a  *= max(1.0, 8.0 / hist_len);
		imageStore(IMG_ASVGF_ATROUS_A, ipos, color_var);

		imageStore(IMG_ASVGF_DEBUG, ipos, vec4(viridis_quintic(alpha_color), 0));
	}
	else {
		const int r = 2; /* spatially compute variance in 5x5 window */
		const int num_pixels = (2 * r + 1) * (2 * r + 1);

		vec3 sum_color   = color_curr.rgb;
		vec2 sum_moments = vec2(lum_curr, lum_curr * lum_curr);

		float sum_w = 1.0;
		float max_lum = luminance(color_curr.rgb);
		for(int yy = -r; yy <= r; yy++) {
			for(int xx = -r; xx <= r; xx++) {
				if(xx == 0 && yy == 0)
					continue;

				ivec2 p = ipos + ivec2(xx, yy);

				float depth  = imageLoad(normalGBuffer, p).w;
 				vec3  normal  = imageLoad(normalGBuffer, p).xyz;
				float dist_z = abs(depth_curr - depth) * motion.a;
				if(dist_z < 2.0) {
					vec4 col_p  = imageLoad(IMG_GBUFFER_ILLUMINATION, p);
					float lum_p = luminance(col_p.rgb);

					float w = pow(max(0.0, dot(normal, normal_curr)), 128.0);
					w *= col_p.a;

					max_lum = max(max_lum, lum_p);
					sum_color   += col_p.rgb * w;
					sum_moments += vec2(lum_p * w, lum_p * lum_p * w * w);
					sum_w       += w;
				}
			}
		}

		sum_color   *= 1.0 / sum_w;
		sum_moments *= vec2(1.0 / sum_w, 1.0 / (sum_w * sum_w));

		vec4 color_var;
	   	color_var.rgb = clamp_color(sum_color.rgb, 128.0);
		/* give variance some boost to make filter more aggressive */
		/* in high variance situations the spatial estimate becomes really bad and
		   unreliable. max seems to work more stable */
		color_var.a   = max_lum * 8;
		//color_var.a   = sqrt(max(sum_moments.y - sum_moments.x * sum_moments.x, 0.0)) * 16.0;

		imageStore(IMG_ASVGF_HIST_MOM, ipos, vec4(sum_moments, 1, 0));
		imageStore(IMG_ASVGF_ATROUS_A, ipos, color_var);

		imageStore(IMG_ASVGF_DEBUG, ipos, vec4(viridis_quintic(1.0), 0));
	}


	//imageStore(IMG_ASVGF_ATROUS_PING, ipos, filtered);

	//return vec4(0); // moments and histlen
}

void
main()
{
	temporal_filter();
	
}
